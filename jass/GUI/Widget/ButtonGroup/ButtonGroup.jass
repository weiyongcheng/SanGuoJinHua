

library ButtonGroup initializer init requires UIBase, ButtonWidget

globals
    private hashtable HT_UI
endglobals


//绘制ButtonGroup, 更新位置信息
// index: button_group  index
// but: 按钮句柄
// r: 行
// c: 列
private function resize takes integer index ,integer but , integer r, integer c returns nothing
    local integer row = LoadInteger(HT_UI, index, 6)
    local integer col = LoadInteger(HT_UI, index, 7)

    local integer button_group = LoadInteger(HT_UI, index, 0)  //ButtonGroup背景
    local real widget_w = LoadReal(HT_UI, index, 2)    //ButtonGroup宽
    local real widget_h = LoadReal(HT_UI, index, 3)     //ButtonGroup高

    local real padding_tb = LoadReal(HT_UI, index, 4)   //背景上下填充
    local real padding_lr = LoadReal(HT_UI, index, 5)   //背景左右填充
    local real margin_tb = LoadReal(HT_UI, index, 8)   //项上下边距
    local real margin_lr = LoadReal(HT_UI, index, 9)   //项左右边距

    local real w =  (widget_w - 2*padding_lr) / col -2*margin_lr   //项宽
    local real h =  (widget_h - 2*padding_tb) / row -2*margin_tb   //项高

    local real x = padding_lr + (widget_w - 2*padding_lr) / col /2  //项起点锚点x
    local real x_step = w + 2*margin_lr //项锚点x步进值
    local real y = padding_tb + (widget_h - 2*padding_tb) / row /2  //项起点锚点y
    local real y_step = h + 2*margin_tb //项锚点y步进值

    call DzFrameSetSize(but, w, h)
    call DzFrameClearAllPoints(but)
    call DzFrameSetPoint(but, 4, button_group, 0, x + x_step*(c-1), -y - y_step*(r-1))
endfunction


//获得一个可用的索引
private function getIndex takes nothing returns integer
    local integer n = 1
    loop
        if not LoadBoolean(HT_UI, n, 0) then
            return n 
        endif
        set n = n +1
    endloop
    return 0
endfunction


//创建ButtonGroup
//  @ButtonGroup 句柄
function CreateButtonGroup takes integer parent, integer r, integer c , real w, real h , real padding_tb, real padding_lr, real margin_tb, real margin_lr returns integer
    local integer fa
    local integer sum = LoadInteger(HT_UI, 0, 0) + 1
    local integer index = getIndex()
    call SaveBoolean(HT_UI, index, 0, true) //占用索引
    call SaveInteger(HT_UI, 0, 0, sum) //创建过的widget数量


    call SaveInteger(HT_UI, index, 6, r)
    call SaveInteger(HT_UI, index, 7, c)
    call SaveReal(HT_UI, index, 2, w)
    call SaveReal(HT_UI, index, 3, h)
    call SaveReal(HT_UI, index, 4, padding_tb)
    call SaveReal(HT_UI, index, 5, padding_lr)
    call SaveReal(HT_UI, index, 8, margin_tb)
    call SaveReal(HT_UI, index, 9, margin_lr)

    if parent == 0  then
        set parent = GAME_UI
    endif

    //创建背景
    set fa = DzCreateFrameByTagName("BACKDROP", "ButtonGroup", parent, "BackdropTemplate", sum)
    call DzFrameSetTexture(fa, "ui\\backdrop-black.blp", 0)
    call DzFrameSetSize(fa, w, h)
    call SaveInteger(HT_UI, index, 0, fa)   //序号->句柄
    call SaveInteger(HT_UI, fa, 0, index)   //句柄->序号

    return fa
endfunction

//创建ButtonGroup
//  button_group: button group 背景句柄
function DestroyButtonGroup takes integer button_group returns nothing
    local integer index = LoadInteger(HT_UI, button_group, 0)
    if index <=0   then
        return
    endif
    call FlushChildHashtable(HT_UI, index)
    call FlushChildHashtable(HT_UI, button_group)
    call DzDestroyFrame(button_group)
endfunction

//添加按钮
//  button_group: button group 背景句柄
// but: 要添加的按钮句柄
// r: 添加位置-行
// c: 添加位置-列
function ButtonGroupAdd takes integer button_group, integer but, integer r, integer c returns nothing
    local integer index = LoadInteger(HT_UI, button_group, 0)
    local string str
    if index <=0  or r <= 0 or c <= 0 then
        return
    endif

    if LoadInteger(HT_UI, button_group, r * c)  != 0    then
        return
    endif

    //保存按钮
    call SaveInteger(HT_UI, button_group, r * c, but)

    //设置大小
    call resize(index, but, r, c)

    //设置高亮贴图
    set str = LoadStr(HT_UI, index, 10)
    if str != null and str != ""    then
        call ButtonWidgetSetBackdrop(but, 4, str)
    endif
    //call MSG("添加按钮")
endfunction

//移除按钮
//  button_group: button group 背景句柄
// r: 移除位置-行
// c: 移除位置-列
function ButtonGroupRemove takes integer button_group, integer r, integer c returns nothing
    local integer index = LoadInteger(HT_UI, button_group, 0)
    local integer but
    if index <=0  or r <= 0 or c <= 0 then
        return
    endif
    set but =  LoadInteger(HT_UI, button_group, r * c)
    if  but == 0    then
        return
    endif
    call DzFrameClearAllPoints(but)
    call SaveInteger(HT_UI, button_group, r * c, 0)

endfunction


//获取按钮控件句柄
//  button_group: button group 背景句柄
// r: 移除位置-行
// c: 移除位置-列
function  ButtonGroupGetButton takes integer button_group, integer r, integer c returns integer
    local integer index = LoadInteger(HT_UI, button_group, 0)
    if index <=0  or r <= 0 or c <= 0 then
        return 0
    endif
    return LoadInteger(HT_UI, button_group, r * c)
endfunction


//设置按钮组内按钮默认的高亮贴图
//  button_group: button group 背景句柄
//  art:贴图路劲
//--该动作之后，添加的按钮将自带该高亮贴图
function ButtonGroupSetHighlightTexture takes integer button_group, string art returns nothing
    local integer index = LoadInteger(HT_UI, button_group, 0)
    local integer but
    if index <=0  then
        return
    endif
    call SaveStr(HT_UI, index, 10, art)

endfunction

private function init takes nothing returns nothing
    set HT_UI = InitHashtable()
endfunction
    
endlibrary